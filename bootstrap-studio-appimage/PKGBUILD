# Maintainer: Shivelight <cwkfr@protonmail.com>

_pkgname=bootstrap-studio

pkgname="${_pkgname}"-appimage
pkgver=5.5.1
pkgrel=1
pkgdesc="A powerful desktop app for creating responsive websites using the Bootstrap framework."
arch=('x86_64')
url="https://bootstrapstudio.io/"
options=(!strip)
_appimage="${pkgname}-${pkgver}.AppImage"
source=("${_appimage}::https://bootstrapstudio.io/releases/desktop/${pkgver}/Bootstrap%20Studio.AppImage")
noextract=("${_appimage}")
sha256sums=('0d25b8eff842909942ea46a153323076ec12eb61d3151c4bb786aae39fa7bf42')

prepare() {
    chmod +x "${_appimage}"
    ./"${_appimage}" --appimage-extract
}

build() {
    # Adjust .desktop so it will work outside of AppImage container
    sed -i -E "s|Exec=AppRun|Exec=/usr/bin/${_pkgname} %F|" \
        "squashfs-root/bstudio.desktop"

    # Fix permissions; .AppImage permissions are 700 for all directories
    chmod -R a-x+rX squashfs-root/usr
}

package() {
    # AppImage
    install -Dm755 "${srcdir}/${_appimage}" "${pkgdir}/opt/${pkgname}/${_pkgname}.AppImage"

    # Desktop file
    install -Dm644 "${srcdir}/squashfs-root/bstudio.desktop" \
            "${pkgdir}/usr/share/applications/${_pkgname}.desktop"

    # Icon images
    install -dm755 "${pkgdir}/usr/share/icons/hicolor/512x512/apps/"
    install -Dm644 "${srcdir}/squashfs-root/usr/share/icons/hicolor/0x0/apps/bstudio.png" \
        "${pkgdir}/usr/share/icons/hicolor/512x512/apps/bstudio.png"

    # Symlink executable
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname}/${_pkgname}.AppImage" "${pkgdir}/usr/bin/${_pkgname}"

}
